%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%	Paper Title:	On a Synergy Between Data and Processes
%	Conference:		BPM'2018
%	Authors:		Jan Martijn E.M. van der Werf, Artem Polyvyanyy, Sietse Overbeek, and Rick Brouwers
%
%	Content:		Constraints of the data model of the running example in the TPTP notation.
%	
%	Copyright:		Artem Polyvyanyy, 2018
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Classes of constraints
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Class 1 (closed universe)		:	a1
% Class 2 (subtype constraints	:	a2, a3, a4
% Class 3 (domain partitioning)	:	a5  .. a9
% Class 4 (relation typing)		:	a10 .. a23
% Class 5 (subset)				:	a24 .. a32
% Class 6 (uniqueness)			:	a33 .. a41
% Class 7 (mandatory)			:	a42
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a1 PROOF FOUND - Entities
fof(entities,				axiom, ( ![X]: ( person(X) | track(X) | course(X) | studyplan(X) | datetime(X) ) ) ). 
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a2 Each Administrator is a Person
fof(admin_is_person,		axiom, ( ![X]: ( administrator(X) => person(X) ) ) ). 
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a3 Each Manager is a Person
fof(manager_is_person,		axiom, ( ![X]: ( manager(X) => person(X) ) ) ). 
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a4 Each Lecturer is a Person
fof(lecturer_is_person,		axiom, ( ![X]: ( lecturer(X) => person(X) ) ) ). 
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a5 PROOF FOUND - Person is Person
fof(person_is_person,		axiom, ( ![X]: ( person(X)		<=> ( ~track(X) & ~course(X) & ~studyplan(X) & ~datetime(X) ) ) ) ). 
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a6 PROOF FOUND - Track is Track
fof(track_is_track,			axiom, ( ![X]: ( track(X)		<=> ( ~person(X) & ~course(X) & ~studyplan(X) & ~datetime(X) ) ) ) ). 
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a7 PROOF FOUND - Course is Course
fof(course_is_course,		axiom, ( ![X]: ( course(X)		<=> ( ~person(X) & ~track(X) & ~studyplan(X) & ~datetime(X) ) ) ) ). 
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a8 PROOF FOUND - StudyPlan is StudyPlan
fof(studyplan_is_studyplan,	axiom, ( ![X]: ( studyplan(X)	<=> ( ~person(X) & ~track(X) & ~course(X) & ~datetime(X) ) ) ) ). 
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a9 PROOF FOUND - DateTime is DateTime
fof(datetime_is_datetime,	axiom, ( ![X]: ( datetime(X)	<=> ( ~person(X) & ~track(X) & ~course(X) & ~studyplan(X) ) ) ) ). 
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a10
fof(registers_types,		axiom, ( ![P,T]: ( registers(P,T) => ( person(P) & track(T) ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a11 PROOF FOUND
fof(enrols_types,			axiom, ( ![P,T]: ( enrols(P,T) => ( person(P) & track(T) ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a12
fof(accepts_types,			axiom, ( ![A,D,P,T]: ( accepts(A,D,P,T) => ( administrator(A) & datetime(D) & person(P) & track(T) ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a13
fof(rejects_types,			axiom, ( ![A,D,P,T]: ( rejects(A,D,P,T) => ( administrator(A) & datetime(D) & person(P) & track(T) ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a14
fof(isof_types,				axiom, ( ![Z,P,T]: ( isof(Z,P,T) => ( studyplan(Z) & person(P) & track(T) ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a15
fof(approves_types,			axiom, ( ![M,Z]: ( approves(M,Z) => ( manager(M) & studyplan(Z) ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a16
fof(enrolsinto_types,		axiom, ( ![P,T,C]: ( enrolsinto(P,T,C) => ( person(P) & track(T) & course(C) ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a17
fof(passes_types,			axiom, ( ![P,T,C,L]: ( passes(P,T,C,L) => ( person(P) & track(T) & course(C) & lecturer(L) ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a18
fof(fails_types,			axiom, ( ![P,T,C,L]: ( fails(P,T,C,L) => ( person(P) & track(T) & course(C) & lecturer(L) ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a19
fof(contains_types,			axiom, ( ![Z,C]: ( contains(Z,C) => ( studyplan(Z) & course(C) ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a20 PROOF FOUND
fof(registersexam_types,	axiom, ( ![P,T]: ( registersexam(P,T) => ( person(P) & track(T) ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a21 PROOF FOUND
fof(awardedexam_types,		axiom, ( ![P,T]: ( awardedexam(P,T) => ( person(P) & track(T) ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a22
fof(validates_types,		axiom, ( ![M,Z]: ( validates(M,Z) => ( manager(M) & studyplan(Z) ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a23
fof(approvesexamof_types,	axiom, ( ![M,P,T]: ( approvesexamof(M,P,T) => ( manager(M) & person(P) & track(T) ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a24
fof(accepts_subset,			axiom, ( ![A,D,P,T]: ( accepts(A,D,P,T) => enrols(P,T) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a25
fof(rejects_subset,			axiom, ( ![A,D,P,T]: ( rejects(A,D,P,T) => registers(P,T) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a26
fof(isof_subset,			axiom, ( ![Z,P,T]: ( isof(Z,P,T) => enrols(P,T) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a27
fof(registersexam_subset,	axiom, ( ![P,T]: ( registersexam(P,T) => enrols(P,T) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a28
fof(awardedexam_subset,		axiom, ( ![P,T]: ( awardedexam(P,T) => enrols(P,T) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a29
fof(approvesexamof_subset,	axiom, ( ![M,P,T]: ( approvesexamof(M,P,T) => enrols(P,T) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a30
fof(enrolsinto_subset,		axiom, ( ![P,T,C]: ( enrolsinto(P,T,C) => enrols(P,T) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a31
fof(passes_subset,			axiom, ( ![P,T,C,L]: ( passes(P,T,C,L) => enrolsinto(P,T,C) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a32
fof(fails_subset,			axiom, ( ![P,T,C,L]: ( fails(P,T,C,L) => enrolsinto(P,T,C) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a33
fof(accepts_unique,			axiom, ( ![X,Y,D,P,T]: ( ( accepts(X,D,P,T) & accepts(Y,D,P,T) ) => ( X=Y ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a34
fof(isof_unique_1,			axiom, ( ![Z,U,V,X,Y]: ( ( isof(Z,U,X) & isof(Z,V,Y) ) => ( U=V & X=Y ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a35
fof(isof_unique_2,			axiom, ( ![X,Y,P,T]: ( ( isof(X,P,T) & isof(Y,P,T) ) => ( X=Y ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a36
fof(rejects_unique,			axiom, ( ![X,Y,D,P,T]: ( ( rejects(X,D,P,T) & rejects(Y,D,P,T) ) => ( X=Y ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a37
fof(approves_unique,		axiom, ( ![X,Y,Z]: ( ( approves(X,Z) & approves(Y,Z) ) => ( X=Y ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a38
fof(validates_unique,		axiom, ( ![X,Y,Z]: ( ( validates(X,Z) & validates(Y,Z) ) => ( X=Y ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a39
fof(passes_unique,			axiom, ( ![P,T,C,X,Y]: ( ( passes(P,T,C,X) & passes(P,T,C,Y) ) => ( X=Y ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a40
fof(fails_unique,			axiom, ( ![P,T,C,X,Y]: ( ( fails(P,T,C,X) & fails(P,T,C,Y) ) => ( X=Y ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a41
fof(approvesexamof_unique,	axiom, ( ![X,Y,P,T]: ( ( approvesexamof(X,P,T) & approvesexamof(Y,P,T) ) => ( X=Y ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% a42
fof(enrols_mandatory,		axiom, ( ![P,T]: ?[A,D]: ( enrols(P,T) => accepts(A,D,P,T) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Extra constraints
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% x1 - An Administrator cannot accept herself.
fof(admins_dont_cheat,		axiom, ( ![P,T,D]: ( ~accepts(P,D,P,T) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% x2 - A Manager cannot approve her StudyPlan.
fof(managers_dont_cheat_1,	axiom, ( ![M,Z]: ( approves(M,Z) => ( ![T]: ( ~isof(Z,M,T) ) ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% x3 - A Lecturer cannot pass a Course for herself.
fof(lecturers_dont_cheat,	axiom, ( ![L,T,C]: ( ~passes(L,T,C,L) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% x4 - A Manager cannot validate her StudyPlan.
fof(managers_dont_cheat_2,	axiom, ( ![M,Z]: ( validates(M,Z) => ( ![T]: ( ~isof(Z,M,T) ) ) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% x5 - A Manager cannot approve an exam of herself.
fof(managers_dont_cheat_3,	axiom, ( ![M,T]: ( ~approvesexamof(M,M,T) ) ) ).
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%